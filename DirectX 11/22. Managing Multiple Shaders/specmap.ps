Texture2D shaderTexture1 : register(t0);
Texture2D shaderTexture2 : register(t1);
Texture2D shaderTexture3 : register(t2);
SamplerState SampleType : register(s0);

cbuffer LightBuffer
{
    float4 diffuseColor;
    float4 specularColor;
    float specularPower;
    float3 lightDirection;
};

struct PixelInputType
{
    float4 position : SV_POSITION;
    float2 tex : TEXCOORD0;
    float3 normal : NORMAL;
    float3 tangent : TANGENT;
    float3 binormal : BINORMAL;
    float3 viewDirection : TEXCOORD1;
};

float4 SpecMapPixelShader(PixelInputType input) : SV_TARGET
{
    float4 textureColor, bumpMap, specMap;
    float3 bumpNormal, lightDir, reflectDir;
    float lightIntensity;
    float4 diffuse = 0.0f;
    float4 specular = 0.0f;
    float4 color;
    
    textureColor = shaderTexture1.Sample(SampleType, input.tex);
    bumpMap = shaderTexture2.Sample(SampleType, input.tex);
    
    bumpMap = (bumpMap * 2.0f) - 1.0f;
    bumpNormal = (bumpMap.x * input.tangent) + (bumpMap.y * input.binormal) + (bumpMap.z * input.normal);
    bumpNormal = normalize(bumpNormal);
    
    // Diffuse
    lightDir = -lightDirection;
    lightIntensity = saturate(dot(lightDir, bumpNormal));
    diffuse = saturate(diffuseColor * lightIntensity);
    diffuse *= textureColor;
    
    if (lightIntensity > 0.0f)
    {
        // Specular
        specMap = shaderTexture3.Sample(SampleType, input.tex);
        reflectDir = normalize(2 * lightIntensity * bumpNormal - lightDir);
        specular = pow(saturate(dot(reflectDir, input.viewDirection)), specularPower);
        specular = specular * specMap;
    }
    
    color = saturate(diffuse+specular);
    return color;
}